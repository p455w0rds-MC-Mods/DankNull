buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
	dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.matthewprenger.cursegradle'

idea{
    module{
        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

sourceCompatibility = targetCompatibility = "1.8"

version = "1.12.2-" + project.modVersion
group = "p455w0rd.danknull"
archivesBaseName = "DankNull"

minecraft {
    version = "1.12.2-14.23.5.2846"
    mappings = "snapshot_20180814"
    runDir = "run"

    replace '@VERSION@', project.modVersion
    replace '@FINGERPRINT@', project.findProperty('signSHA1')
    serverJvmArgs += "-Dfml.doNotBackup=true"
    serverJvmArgs += "-Dfml.queryResult=confirm"
    clientJvmArgs += "-Dfml.doNotBackup=true"
    clientRunArgs += "-username=Dev"
}

repositories {
    maven { url 'https://cursemaven.com' }
}

dependencies {
    deobfCompile "curse.maven:p455w0rds-library-255232:2830265"

    deobfProvided "curse.maven:the-one-probe-245211:2667280"
    deobfProvided "curse.maven:jei-238222:2803400"
    deobfProvided "curse.maven:hwyla-253449:2568753"
    deobfProvided "curse.maven:chisel-235279:2915375"
    deobfProvided "curse.maven:ctm-267602:2915363"
    deobfProvided "curse.maven:extra-utilities-225561:2678374"
    deobfProvided "curse.maven:not-enough-items-247694:2755792"
    deobfProvided "curse.maven:codechicken-242818:2779848"
    deobfProvided "curse.maven:item-scroller-242064:2581199"

    deobfProvided "curse.maven:overloaded-264601:2875265"

    testCompile 'junit:junit:4.12'
    testCompile "com.google.truth:truth:+"
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        project.hasProperty('keyStore')
    }

    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

reobfJar.finalizedBy(signJar)

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

// Make Gradle not Cache these so they always run
runClient.outputs.upToDateWhen { false }
runServer.outputs.upToDateWhen { false }